	SIMULATE
	
	INITIAL X$ARM2,0
	INITIAL X$CVK,0
	
FCVK	FUNCTION RN1,D2
.8,45/1,35
KMMTK1	FUNCTION RN1,D3
.1,3/.7,1/1,2
KMMTK2	FUNCTION RN1,D2
.2,4/1,3
FKMM	FUNCTION P1,E3
1,FN$KMMTK1/2,FN$KMMTK2/3,2

gotoTER	TERMINATE
	
	GENERATE 150,47,,,20
	ASSIGN 1,1
	ASSIGN 2,0
gotoT1	QUEUE TERMQ1
	SEIZE STERM1
	DEPART TERMQ1
	ADVANCE 28,10
	RELEASE STERM1
	
	QUEUE QARM1
	TEST L Q$SARM1,1,gotoTER
	SEIZE SARM1
	DEPART QARM1
	ADVANCE 28,3
	RELEASE SARM1
	TRANSFER .5,gotoT1,gotoKMM
	
	GENERATE 95,15,,42,20
	ASSIGN 1,2
	ASSIGN 2,0
gotoT2	QUEUE TERMQ2
	SEIZE STERM2
	DEPART TERMQ2
	ADVANCE 32,15
	RELEASE STERM2
	
	QUEUE QARM2
	TEST L Q$SARM2,1,gotoTER
	SEIZE SARM2
	DEPART QARM2
	ADVANCE 25,4
	RELEASE SARM2
	TEST L X$ARM2,20,gotoTRN
	SAVEVALUE ARM2+,1
gotoTRN	TRANSFER .5,gotoT2,gotoKMM

	GENERATE 125,40,,37,20
	ASSIGN 1,3
	ASSIGN 2,0
gotoT3	QUEUE TERMQ3
	SEIZE STERM3
	DEPART TERMQ3
	ADVANCE 30,18
	RELEASE STERM3
	
	QUEUE QARM3
	TEST L Q$SARM3,1,gotoTER
	SEIZE SARM3
	DEPART QARM3
	ADVANCE 27,4
	RELEASE SARM3
	TRANSFER .5,gotoT3,gotoKMM
	
gotoKMM	QUEUE QKMM
	SEIZE SKMM
	DEPART QKMM
	ADVANCE FN$FKMM
	RELEASE SKMM
	TEST E P2,0,gtT123
	QUEUE QCVK
	SEIZE SCVK
	DEPART QCVK
	ADVANCE FN$FCVK
	RELEASE SCVK
	TEST L X$ARM2,20,gotoASG
	SAVEVALUE CVK+,1
gotoASG	ASSIGN 2,1
	PRIORITY 10
	TRANSFER ,gotoKMM
	
gtT123	PRIORITY 20
	ASSIGN 2,0
	TEST E P1,1,gtT2
	TRANSFER ,gotoT1
gtT2	TEST E P1,2,gotoT3
	TRANSFER ,gotoT2
	
	GENERATE 2800
	TERMINATE 1
	START 1	
	END
	
